competency questions:
  Data Management:
    - question: "Can the hierarchical structure of time series segments be retrieved?"
      example: "Retrieve parent-child relationships between segments."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?segment ?subSegment
        WHERE {
          ?subSegment semts:isPartOf ?segment .
        }
    - question: "Can all dimensions of time series segments be retrieved?"
      example: "Retrieve dimensions of segments with associated properties."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?segment ?dimension
        WHERE {
          ?segment a semts:TimeSeriesSegment .
          ?segment semts:segmentDimension ?dimension .
          FILTER EXISTS { ?dimension ?p ?o } .
        }
    - question: "Is it possible to identify segment metadata?"
      example: "Get title, dataset column index and time index of 5 segments."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?segment ?title ?datasetColumnIndex ?timeIndex
        WHERE {
            ?segment semts:title ?title .
            ?segment semts:datasetColumnIndex ?datasetColumnIndex .
            ?segment semts:segmentIndex ?timeIndex .
        }
        LIMIT 5
    - question: "Is it possible to retrieve details on a knowledge value associated with a segment?"
      example: "Get the datatype of an arbitrary knowledge value."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>
        PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
        PREFIX qudt: <http://qudt.org/schema/qudt#>

        SELECT ?knowledge ?value ?datatype
        WHERE {
            ?segment semts:segmentKnowledge ?knowledge .
            ?knowledge semts:hasValue ?value .
            ?value semts:hasDatatype ?datatype .
            OPTIONAL { ?datatype rdf:type qudt:Datatype }
        }
        LIMIT 1
    - question: "Is it possible to identify all segments within a particular time window?"
      example: "Retrieve all segments between '2024-01-01T00:00:00' and '2024-01-09T07:59:59'."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>
        PREFIX time: <http://www.w3.org/2006/time#>
        PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

        SELECT ?segment ?startTime ?endTime
        WHERE {
            ?segment semts:segmentIndex ?timeEntity .
            
            {
                ?timeEntity a time:DateTimeInterval ;
                          time:hasBeginning ?startPoint ;
                          time:hasEnd ?endPoint .
                ?startPoint time:inXSDDateTime ?startTime .
                ?endPoint time:inXSDDateTime ?endTime .
                
                FILTER(?startTime >= "2024-01-01T00:00:00"^^xsd:dateTime && 
                       ?endTime <= "2024-01-09T07:59:59"^^xsd:dateTime)
            }
            UNION
            {
                ?timeEntity a time:Instant ;
                          time:inXSDDateTime ?pointTime .
                
                BIND(?pointTime AS ?startTime)
                BIND(?pointTime AS ?endTime)
                
                FILTER(?pointTime >= "2024-01-01T00:00:00"^^xsd:dateTime && 
                       ?pointTime <= "2024-01-09T07:59:59"^^xsd:dateTime)
            }
        }
  Knowledge Classification:
    - question: "Can all knowledge categorized as 'Data Knowledge' be retrieved?"
      example: "Retrieve all knowledge instances directly derived from data."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?knowledge
        WHERE {
          ?knowledge a semts:DataKnowledge .
        }
    - question: "Can all knowledge categorized under 'Method Knowledge' be retrieved?"
      example: "Retrieve knowledge about optimal method presets."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?knowledge
        WHERE {
          ?knowledge a semts:MethodKnowledge .
        }
    - question: "Can all knowledge categorized under 'Scenario Knowledge' be retrieved?"
      example: "Retrieve all domain-specific knowledge instances."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?knowledge
        WHERE {
          ?knowledge a semts:ScenarioKnowledge .
        }
    - question: "Can quality measures for specific knowledge entities be retrieved?"
      example: "Retrieve the quality measure of knowledge instances."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?knowledge ?qualityMeasure
        WHERE {
          ?knowledge a semts:Knowledge .
          ?knowledge semts:knowledgeQualityMeasure ?qualityMeasure .
        }
    - question: "Can reasoning-based knowledge generation processes be identified?"
      example: "Retrieve all reasoning processes used to validate knowledge."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?reasoning
        WHERE {
          ?reasoning a semts:KnowledgeReasoning .
        }
    - question: "Can knowledge quality measures and their associated concepts be retrieved?"
      example: "Retrieve all evaluation metrics for knowledge quality."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?qualityMeasure ?concept
        WHERE {
          ?qualityMeasure a semts:KnowledgeQualityMeasure .
          ?qualityMeasure semts:qualityMeasureConcept ?concept .
        }
    - question: "Is it possible to retrieve any segment representing particular data knowledge?"
      example: "Retrieve two segments having a positive linear trend with slope greater or equal to 1."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>
        PREFIX semts-dv: <https://w3id.org/semts/vocabulary/data-knowledge#>

        SELECT ?segment ?valueString ?datasetColumnIndex
        WHERE {
            ?segment semts:segmentKnowledge ?knowledge .
            ?segment semts:datasetColumnIndex ?datasetColumnIndex .
            ?knowledge semts:knowledgeConcept semts-dv:LinearSlope .
            ?knowledge semts:hasValue ?value .
            ?value semts:valueString ?valueString .
            FILTER (xsd:float(?valueString) > 1)
        }
        LIMIT 2
    - question: "Is it possible to identify all knowledge concepts occurring in a certain time frame of a time series?"
      example: "Retrieve all knowledge concepts associated with segments overlapping with time '2024-01-02T12:00:00'."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>
        PREFIX time: <http://www.w3.org/2006/time#>

        SELECT DISTINCT ?concept
        WHERE {    
            ?segment a semts:TimeSeriesSegment ;
                     semts:segmentIndex ?timeEntity ;
                     semts:segmentKnowledge ?knowledge .

            ?knowledge semts:knowledgeConcept ?concept .
            
            {
                ?timeEntity a time:DateTimeInterval ;
                          time:hasBeginning ?startPoint ;
                          time:hasEnd ?endPoint .
                ?startPoint time:inXSDDateTime ?startTime .
                ?endPoint time:inXSDDateTime ?endTime .
                
                FILTER("2024-01-02T12:00:00"^^xsd:dateTime >= ?startTime && "2024-01-02T12:00:00"^^xsd:dateTime <= ?endTime)
            }
            UNION
            {
                ?timeEntity a time:Instant ;
                          time:inXSDDateTime ?pointTime .
                BIND(?pointTime AS ?startTime)
                BIND(?pointTime AS ?endTime)
                FILTER("2024-01-02T12:00:00"^^xsd:dateTime = ?pointTime)
            }

        }
    - question: "Is it possible to filter the data based on scenario knowledge?"
      example: "Retrieve all segments which are annotated by a label."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>
        PREFIX semts-sv: <https://w3id.org/semts/vocabulary/scenario-knowledge#>

        SELECT ?segment ?datasetColumnIndex
        WHERE {
            ?segment semts:segmentKnowledge ?knowledge .
            ?segment semts:datasetColumnIndex ?datasetColumnIndex .
            ?knowledge semts:knowledgeConcept semts-sv:Label .
        }
  Knowledge Generation:
    - question: "Can all generated knowledge and their provenance be retrieved?"
      example: "Retrieve knowledge entities and their generating entities."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?output ?knowledge
        WHERE {
          ?output a semts:KnowledgeGenerationOutput .
          ?output semts:outputKnowledge ?knowledge .
        }
    - question: "Can knowledge describing specifics of an algorithm be retrieved?"
      example: "Retrieve knowledge defining algorithms."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?knowledge
        WHERE {
          ?algorithm a semts:Algorithm .
          ?generationEntity semts:knowledgeGenerationConcept ?algorithm .
          ?knowledge semts:knowledgeGenerationEntity ?generationEntity .
        }
    - question: "Can all embedded values in the knowledge graph be retrieved?"
      example: "Retrieve embedded values such as mean or variance."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?value
        WHERE {
          ?value a semts:EmbeddedValue .
        }
    - question: "Can referenced values and their description be retrieved?"
      example: "Retrieve referenced values stored externally and their descriptions."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?value ?description
        WHERE {
          ?value a semts:ReferencedValue .
          ?value semts:description ?description .
        }
    - question: "Is it possible to retrieve knowledge that was generated by a particular analysis method?"
      example: "Retrieve all knowledge instances generated by an Autoencoder."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>
        PREFIX mlso: <http://w3id.org/mlso/vocab/ml_algorithm#>
        PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

        SELECT ?knowledge
        WHERE {
          ?knowledge semts:knowledgeGenerationEntity ?generationEntity .
          ?generationEntity semts:knowledgeGenerationConcept mlso:AutoEncoder .
        }
    - question: "Is it possible to list all segments that contributed to the generation of knowledge?"
      example: "List all segments which served as input to the generation of knowledge."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>
        PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

        SELECT DISTINCT ?segment ?knowledgeGeneration
        WHERE {
          ?knowledgeGeneration semts:used ?input .
          ?input rdf:type semts:KnowledgeGenerationInput .
          ?input semts:inputDataReference ?segment .
        }
    - question: "Is it possible to retrieve knowledge based on specifics from the generation process?"
      example: "Retrieve any prior knowledge, which means any knowledge that was not derived from raw data or other knowledge."
      query: |
        PREFIX semts: <https://w3id.org/semts/ontology#>

        SELECT ?knowledge ?generationType
        WHERE {
          ?segment semts:segmentKnowledge ?knowledge
          FILTER NOT EXISTS { ?knowledge semts:knowledgeGenerationEntity ?entity }
          
          BIND("No Generation Entity" AS ?generationType)
        }